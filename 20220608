-- 供应商分割
select top 10
	a.ItemID,
	info = max(case when ElementID = 'b31c2cf3-6dcd-4ffa-88a5-778bdc1f827a' then convert(varchar(max), [value]) else null end)
--	info = max(case when ElementID = '7b60f360-a3e4-491c-997e-8ffbd2b10d47' then convert(VARCHAR(max), [value]) else null end)
into #c1
from (select * from [web2].[OA2].[dbo].[WF_Values] where status = 1 or status is null) b
right join (select * from [web2].[OA2].[dbo].[WF_Items] where [TemplateID] = '2d1d37a7-c5eb-44a6-ad0a-cfff30a3e5e2') a on a.ItemID = b.ItemID
--right join (select * from [web2].[OA2].[dbo].[WF_Items] where [TemplateID]='3761dd1c-be85-43be-a3ef-8b00deb7d2b5') a on a.ItemID=b.ItemID
where a.[Status] in (1, 3)
group by a.ItemID

-- info分割 分割推荐供应商 每家供应商占一行
select ItemID, b.info, row_number()over(order by ItemID desc) xh
into #tb1
from (
	select 
		ItemID,
		info = convert(xml, '<root><v>' + replace(info, '],', '</v><v>') + '</v></root>')
	from #c1) a
outer apply (
	select info = N.v.value('.', 'varchar(max)')
	from a.info.nodes('/root/v') N(v)) b 
	
-- info2分割 细节分割
select ItemID, xh, b.info
into #tb2
from (
	select ItemID, xh, info = convert(xml, '<root><v>' + replace(info, '","', '</v><v>') + '</v></root>')
	from #tb1) a
outer apply (
	select info = N.v.value('.', 'varchar(max)')
	from a.info.nodes('/root/v') N(v)) b
	
update #tb2 set info = replace(info, '[["', '')
update #tb2 set info = replace(info, '["', '')
	
-- 行拼接
declare @tsql1 varchar(2000), @tsql2 varchar(2000), @tsql3 varchar(8000)
					
select @tsql1 = isnull(@tsql1 + ',', '') + '[' + rtrim(number) + ']',
	   @tsql2 = isnull(@tsql2 + ',', '') + 'isnull([' + rtrim(number) + '],'''') ''字段' +rtrim(number)+''''
from master.dbo.spt_values 
where type = 'P' 
	and number >= 1 
	and number <= (select top 1 count(1) from #tb2 group by xh order by count(1) desc)

select @tsql3 = 'select ItemID ''ItemID'', row_number()over(partition by ItemID order by ItemID) xxh, ' + @tsql2
	+ ' into ##gys'
	+ ' from (select ItemID, xh, info, row_number()over(partition by xh order by getdate()) rn from #tb2) t '
	+ ' pivot(max(info) for rn in(' + @tsql1 + ')) u '
	+ ' order by ItemID desc '
exec(@tsql3)


--------------------

-- 获取供应商名称 横着排 放在预算金额后面
set @tsql1 = null
set @tsql2 = null
set @tsql3 = null
					
select @tsql1 = isnull(@tsql1 + ',', '') + '[' + rtrim(number) + ']',
	   @tsql2 = isnull(@tsql2 + ',', '') + 'isnull([' + rtrim(number) + '],'''') ''供应商名称'+rtrim(number)+''''
from master.dbo.spt_values 
where type = 'P' 
	and number >= 1 
	and number <= (select top 1 count(1) from ##gys group by ItemID order by count(1) desc)

select @tsql3 = 'select ItemID ''ItemID'', ' + @tsql2
	+ ' into ##gys1'
	+ ' from (select ItemID, 字段1, row_number()over(partition by ItemID order by getdate()) rn from ##gys) t '
	+ ' pivot(max(字段1) for rn in(' + @tsql1 + ')) u '
	+ ' order by ItemID desc '
exec(@tsql3)

--------------------

-- 第二个表gys2创建
set @tsql1 = null
set @tsql2 = null
set @tsql3 = null

select ItemID, b.info
into #tb3
from (
	select 
		ItemID,
		info = convert(xml, '<root><v>' + replace(replace(info, '],', '</v><v>'), '","', '</v><v>') + '</v></root>')
	from #c1) a
outer apply (
	select info = N.v.value('.', 'varchar(max)')
	from a.info.nodes('/root/v') N(v)) b 
	
update #tb3 set info = replace(info, '[', '')
update #tb3 set info = replace(info, ']', '')
update #tb3 set info = replace(info, '"', '')
	
						
select @tsql1 = isnull(@tsql1 + ',', '') + '[' + rtrim(number) + ']',
	   @tsql2 = isnull(@tsql2 + ',', '') + 'isnull([' + rtrim(number) + '],'''') ''字段' +rtrim(number)+''''
from master.dbo.spt_values 
where type = 'P' 
	and number >= 1 
	and number <= (select top 1 count(1) from #tb3 group by ItemID order by count(1) desc)

select @tsql3 = 'select ItemID ''ItemID'', row_number()over(partition by ItemID order by ItemID) xxh, ' + @tsql2
	+ ' into ##gys2'
	+ ' from (select ItemID, info, row_number()over(partition by ItemID order by getdate()) rn from #tb3) t '
	+ ' pivot(max(info) for rn in(' + @tsql1 + ')) u '
	+ ' order by ItemID desc '
exec(@tsql3)



-- 获取全部信息
select 
	凭证号 = a.ItemID,
	申请单位 = max(case when ElementID = '47dea0c9-f108-4345-b10a-16b2e96ded3d' then convert(varchar(100), [value]) else null end),
	[物料/项目名称] = max(case when ElementID = '58ca53c4-c77c-4b85-9ea2-3bc7ec46869c' then convert(varchar(200), [value]) else null end),
	项目主要内容 = max(case when ElementID = 'e4bf6978-1687-4924-a71d-187888938b14' then convert(varchar(2000), [value]) else null end),
	[预算金额（不含税）] = max(case when ElementID = '4d468c98-bc0c-4d99-ae1b-426479e46998' then convert(varchar(100), [value]) else null end),
	供应商选择方法 = max(case when ElementID = 'f72a9992-a5f8-4b8b-aff5-3fe7a38dc98a' then convert(varchar(200), [value]) else null end),
	申请人 = max(case when ElementID = '1372609b-58f0-4e26-a30d-7f49f55fb5aa' then convert(varchar(100), [value]) else null end),
	紧急采购 = max(case when ElementID = '63b79e56-69b7-4cbc-b561-399c65b6d9e1' then convert(varchar(200), [value]) else null end),
	独家供应商采购 = max(case when ElementID = '6565bb97-3ea6-432b-a9ee-35200a45507b' then convert(varchar(200), [value]) else null end),
	独家供应商采购理由 = max(case when ElementID = '6db8a9d0-92fb-40d9-933f-4146c89846b2' then convert(varchar(max), [value]) else null end),
	独家推荐供应商具体说明 = max(case when ElementID = '0dec8165-9cd0-45dd-99b4-538326784ba6' then convert(varchar(max), [value]) else null end),
	业务类型 = max(case when ElementID = '36f437b3-0bc5-48e7-a122-ac0e330572c5' then convert(varchar(200), [value]) else null end),
	申请采购理由 = max(case when ElementID = 'b2e19b77-e564-4b02-8983-51704f4c4b48' then convert(varchar(max), [value]) else null end),
	实施地点 = max(case when ElementID = '77167598-2e03-4a7d-aac0-a466eb006040' then convert(varchar(200), [value]) else null end),
	计划开工日期 = max(case when ElementID = '6e3e39d4-65cc-40d0-8b64-8d8726f1e168' then convert(varchar(200), [value]) else null end),
	预计工期 = max(case when ElementID = '8d8e0015-d2b2-4cec-9928-41066e090974' then convert(varchar(200), [value]) else null end),
	[投资项目号/费用预算号或资金科目] = max(case when ElementID = '13e87e74-d82f-4c51-a227-8fb917e1391d' then convert(varchar(200), [value]) else null end),
	供应商必要条件 = max(case when ElementID = 'af997ab1-c377-4eee-9584-bb510dc4b862' then convert(varchar(200), [value]) else null end),
	中选供应商 = max(case when ElementID = '2f98e0f1-e103-4e9b-83af-38f4f9af38cf' then convert(varchar(200), [value]) else null end),
	发起申请时间 = max(CONVERT(varchar(100),a.[CreateDateTime],20)),
	最后一个领导审批结束时间 = max(case when b.NodeID = '67439cc4-9c2b-4cca-9055-73267c4e7b93' then CONVERT(varchar(100),[DateTime],20) else null end),
	采购部接收时间 = max(case when b.NodeID = '89f4bc54-1253-44ab-9b68-fdf19eb7c73e' then CONVERT(varchar(100),[DateTime],20) else null end),
	采购部反馈用户时间 = ''
into #tb
from (select * from [web2].[OA2].[dbo].[WF_Values] where status = 1 or status is null) b
right join (select * from [web2].[OA2].[dbo].[WF_Items] where [TemplateID] = '2d1d37a7-c5eb-44a6-ad0a-cfff30a3e5e2') a on a.ItemID = b.ItemID
where a.[Status] in (1, 3)
group by a.ItemID
order by a.ItemID desc


-- 把各个查询得到的临时表结合
select a.*, g1.*, g2.*
from #tb a
left join ##gys1 g1 on a.凭证号 = g1.ItemID
left join ##gys2 g2 on a.凭证号 = g2.ItemID
-- 最后记得把g1.ItemID隐藏了
